plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.spring.dependency-management" version "1.1.7"
    id 'io.freefair.lombok' version '8.13.1'
    id 'jacoco'
    id "com.diffplug.spotless" version "7.0.3"
}

group = 'me.desair.tus'
version = '1.0.0-3.1-SNAPSHOT'
description = 'me.desair.tus:tus-java-server'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom 'org.springframework.boot:spring-boot-dependencies:3.4.5'
    }
}

dependencies {
    implementation 'org.apache.commons:commons-lang3'
    implementation 'commons-io:commons-io:2.19.0'
    implementation 'commons-codec:commons-codec:1.18.0'
    implementation 'org.slf4j:slf4j-api'
    implementation 'jakarta.servlet:jakarta.servlet-api'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.19.0'

    testImplementation 'ch.qos.logback:logback-classic'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-json'
    testImplementation 'org.springframework:spring-web'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.hamcrest:hamcrest:3.0'
}

tasks.withType(JavaCompile) {
    options.compilerArgs.add("-parameters")
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
    }
}

spotless {
    java {
        googleJavaFormat().reflowLongStrings()
    }
}